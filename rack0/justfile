# compose operations over {profiles, svcs}

alias b := bounce
alias f := follow
alias s := sync
alias p := ps
profile := "'*'"

up profile=profile *svcs:
    docker compose --profile {{ profile }} up -d --remove-orphans --pull missing {{ svcs }}

pullup profile=profile *svcs:
    docker compose --profile {{ profile }} up -d --remove-orphans --pull always {{ svcs }}

down profile=profile *svcs:
    docker compose --profile {{ profile }} down {{ svcs }}

bounce profile=profile *svcs:
    #!/bin/bash
    docker compose --profile {{ profile }} down {{ svcs }} && \
    docker compose --profile {{ profile }} up -d --remove-orphans {{ svcs }}

build *svcs:
    docker compose build --push {{ svcs }}

run *svcs:
    docker compose run --rm -it {{ svcs }}

log *args:
    docker compose logs {{ args }}

follow *args:
    just log --follow {{ args }}

attach svc cmd="/bin/sh":
    docker compose exec -it {{ svc }} {{ cmd }}

ps *svcs:
    docker compose ps {{ svcs }}

watch:
    watch -n 1 "docker compose ps"

bbounce profile=profile *svcs:
    #!/bin/bash
    docker compose --profile {{ profile }} down {{ svcs }} || true \
    && docker compose build --push {{ svcs }} \
    && docker compose --profile {{ profile }} up -d --remove-orphans --pull always {{ svcs }}

pbounce profile=profile *svcs:
    #!/bin/bash
    docker compose pull && \
    just bounce "{{ profile }}" {{ svcs }}

# logins, keys, helpers

keys:
    echo "$(op read 'op://homelab/dkey/public key')" | $DHOST_SSH "cat >> ~/.ssh/authorized_keys"

login:
    #!/bin/bash
    echo $PASS | docker login ${REGISTRY} -u $USER --password-stdin
    echo $DOCKERHUB_PASS | docker login docker.io -u $DOCKERHUB_USER --password-stdin

rlogin:
    #!/bin/bash
    $DHOST_SSH "echo $PASS | docker login ${REGISTRY} -u $USER --password-stdin"
    $DHOST_SSH "echo $DOCKERHUB_PASS | docker login docker.io -u $DOCKERHUB_USER --password-stdin"

sync message="sync":
    git commit -a -m "{{ message }}" || true && git pull && git push
    $DHOST_SSH -At "cd ${REPO} && git commit -a -m '{{ message }}' || true && git pull && git push"

rsync message="sync":
    $DHOST_SSH -At "cd ${REPO} && git commit -a -m '{{ message }}' || true && git pull && git push"
    git commit -a -m "{{ message }}" || true && git pull && git push

reset:
    $DHOST_SSH -At "cd ${REPO} && git fetch origin"
    $DHOST_SSH -At "cd ${REPO} && git reset --hard origin/main"

ssh:
    $DHOST_SSH -t "$(op read op://homelab/svc/bash) && cd ${REPO_MACHINE} && bash -l"

cm:
    $DHOST_SSH -t "$(op read op://homelab/svc/bash) && bash -i -c 'just cm'"

# service-specific recipes

sync_dns:
    just sync
    just bounce core blocky
